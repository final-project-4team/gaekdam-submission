plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.1'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

group = 'org.example'
version = '0.0.1-SNAPSHOT'
description = 'gaekdam-be'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    mockitoAgent
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:3.0.5'
    testImplementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter-test:3.0.5'

    implementation 'org.springframework.boot:spring-boot-starter-flyway'
    implementation 'org.flywaydb:flyway-mysql'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-webmvc'
    testImplementation 'org.springframework.security:spring-security-test'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-data-jpa-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'
    testImplementation 'org.junit.platform:junit-platform-suite'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // 문자메세지
    implementation 'com.solapi:sdk:1.0.3'


    // dek 캐싱
    implementation 'com.github.ben-manes.caffeine:caffeine:3.1.8'

    //스프링 시큐리티
    implementation 'org.springframework.boot:spring-boot-starter-security'

    //redis(임시)
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    //jwt
    implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    implementation 'org.springframework.boot:spring-boot-starter-validation'
    // excel
    implementation 'org.apache.poi:poi-ooxml:5.5.1'
    // csv
    implementation 'com.opencsv:opencsv:5.12.0'

    //jakarta servlet
    compileOnly 'jakarta.servlet:jakarta.servlet-api:6.0.0'

    //SMTP
    implementation 'org.springframework.boot:spring-boot-starter-mail'

    // integrationTest(Testcontainers)
    testImplementation platform("org.testcontainers:testcontainers-bom:1.21.4")
    testImplementation "org.testcontainers:junit-jupiter"
    testImplementation "org.testcontainers:mariadb"

    mockitoAgent "net.bytebuddy:byte-buddy-agent:1.17.8"
    //swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.4'

}

tasks.named('test') {
    useJUnitPlatform {
        excludeTags 'dummy'
    }
    jvmArgs "-javaagent:${configurations.mockitoAgent.singleFile}"
}

jacoco {
    toolVersion = "0.8.12"
}

tasks.named('jacocoTestReport') {
    dependsOn tasks.named('test')

    def includes = [
            "com/gaekdam/gaekdambe/**/*Service.class"
    ]

    def excludes = [
            "**/*Application*",
            "**/*Config*",
            "**/global/config/**",
            "**/global/exception/**",
            "**/global/paging/**",
            "**/dto/**",
            "**/controller/**",
            "**/*Mapper*",
            "**/repository/**",
            "**/infrastructure/**",
            "**/service/model/**",
            "**/service/assembler/**",
            "**/service/**/row/**"
    ]

    // 전체 class 대상으로 report 만들고 excludes만 적용
    classDirectories.setFrom(
            sourceSets.main.output.asFileTree.matching {
                include includes
                exclude excludes
            }
    )


    reports {
        html.required = true
        xml.required = false
        csv.required = false
    }
}

// integrationTest sourceSet + task
sourceSets {
    integrationTest {
        java.srcDir file('src/integrationTest/java')
        resources.srcDir file('src/integrationTest/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    useJUnitPlatform()
    shouldRunAfter tasks.named('test')
    jvmArgs "-javaagent:${configurations.mockitoAgent.singleFile}"
}

// dummy data generator (local only)
tasks.register('dummyData', Test) {
    description = 'Runs dummy data generator tests (local DB only).'
    group = 'verification'

    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath

    useJUnitPlatform {
        includeTags 'dummy'
    }

    jvmArgs "-javaagent:${configurations.mockitoAgent.singleFile}"
}


